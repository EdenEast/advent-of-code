alias b := bench
alias r := run
alias m := mrun
alias p := puzzle
alias s := submit
alias t := test
alias w := watch

default:
  just --choose

run day part:
  (cd ../../common/rust/aoc; cargo run --release -- run {{day}} {{part}} 2024)

drun day part:
  (cd ../../common/rust/aoc; cargo run -- run {{day}} {{part}} 2024)

mrun day part:
  (cd ../../common/rust/aoc; cargo run --release -- --minimal run {{day}} {{part}} 2024)

test day part:
  (cd ../../common/rust/aoc; cargo run -- test {{day}} {{part}} 2024)

watch:
  watchexec -c clear -w src cargo test

bench day:
  cargo bench --bench day_{{day}}

submit day part:
  #!/usr/bin/env bash
  cd ../../common/rust/aoc
  answer=$(cargo run --release -- --minimal run {{day}} {{part}} 2024)
  aoc submit --day {{day}} --year 2024 {{part}} "$answer"

puzzle day:
  (cd ../..; just puzzle {{day}} 2024)

new:
  #!/usr/bin/env bash
  last=$(fd day_ src | cut -c 9- | cut -c 1-2 | tail -n 1)
  ((last++))
  last=$(printf "%02d" $last)
  cp ./template/benches/day_xx.rs ./benches/day_${last}.rs
  sd "XX" "$last" ./benches/day_${last}.rs
  echo "" >> ./Cargo.toml
  echo "[[bench]]" >> ./Cargo.toml
  echo "name = \"day_${last}\"" >> ./Cargo.toml
  echo "harness = false" >> ./Cargo.toml
  cp ./template/src/day_xx.rs ./src/day_${last}.rs
  sd "XX" "$last" ./src/day_${last}.rs
  nvim ./src/lib.rs -s ./template/append_to_lib


